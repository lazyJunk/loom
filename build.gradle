plugins {
    id 'java'
    id 'application'
}

group 'org.example'
version '1.0-SNAPSHOT'

project.ext.lwjglVersion = "3.3.1-SNAPSHOT"
project.ext.lwjglNatives = "natives-windows"

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
    implementation 'org.jetbrains:annotations:20.1.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-assimp"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-openal"
    implementation "org.lwjgl:lwjgl-opengl"
    implementation "org.lwjgl:lwjgl-stb"
    runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-assimp::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-openal::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"

    implementation "org.joml:joml:1.10.3"
    implementation 'com.github.jhg023:SimpleNet:1.6.6'

    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.12.2"
    implementation "org.jooq:joou:0.9.4"
}

test {
    useJUnitPlatform()
}

application {
    mainClass.set("lazy.loom.game.client.Client")
}

jar {
    manifest {
        attributes 'Main-Class': application.mainClassName
    }
}

task uberJar(type: Jar) {
    archiveClassifier = 'uber'

    from sourceSets.main.output
    duplicatesStrategy(DuplicatesStrategy.INCLUDE)
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
    manifest {
        attributes 'Main-Class': application.mainClassName
    }
}